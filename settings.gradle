// Workaround for project properties not populated yet
def props  = getGradle().getStartParameter().getProjectProperties()
def sysProps = System.getProperty("target")
def target = props["target"] ?: sysProps

def useJS = target == "js"
def useNative = target == "native"


include 'koma-core'

if (useJS) {
    rootProject.buildFileName = 'buildscripts/build-js.gradle'
}
else if (useNative) {
    def sysprops = System.getProperties()
    if (sysprops['idea.platform.prefix'] != null) {
        // We're being run from intellij, need a faux java project to enable completion
        rootProject.buildFileName = "buildscripts/build-native-intellij.gradle"
    } else {
        // We're being run from command line, use native toolchain
        rootProject.buildFileName = "buildscripts/build-native-cmdline.gradle"
    }
}
else {
    rootProject.buildFileName = "buildscripts/build-jvm.gradle"
    
    include 'koma-logging'
    include 'backend-matrix-ejml'
    include 'backend-matrix-mtj'
    include 'backend-matrix-jblas'
    include 'koma-tests'
}

