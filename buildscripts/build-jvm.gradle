buildscript {
    ext.kotlin_version = '1.1.2'
    ext.project_version = '0.9'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply from: "buildscripts/publishing.gradle"
apply from: "buildscripts/codegen.gradle"

def subproj(project) {
    project.with {
        apply plugin: 'kotlin'
        sourceSets {
            main.kotlin.srcDirs = ['src']
            test.kotlin.srcDirs = ['test']
        }
        repositories {
            mavenCentral()
        }
        dependencies {
            compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        }
        configurePublishing()
    }
}

project(':koma-core') {
    subproj(project)
    
    sourceSets.main.kotlin.srcDirs = ['src', 'srcjvm', 'external/js', 'srcgen']

    compileKotlin.dependsOn(":codegen")

    dependencies {
        compile "org.slf4j:slf4j-api:1.7.21"
        compile "org.knowm.xchart:xchart:3.1.0"
    }
}

project(':koma-logging') {
    subproj(project)

    dependencies {
        compile project(":koma-core")
        compile "org.slf4j:slf4j-api:1.7.21"
        compile "ch.qos.logback:logback-classic:1.1.7"
    }
}

project(':backend-matrix-mtj') {
    subproj(project)

    dependencies {
        compile project(":koma-core")
        compile("com.googlecode.matrix-toolkits-java:mtj:1.0.4") {
            // Fix issue with pom-only artifacts in MTJ
            exclude module: "com.github.fommil.netlib:all:pom:1.1.2"
        }
    }
}

project(':backend-matrix-ejml') {
    subproj(project)

    dependencies {
        compile project(":koma-core")
        compile "org.ejml:all:0.27"
    }
}

project(':backend-matrix-jblas') {
    subproj(project)

    dependencies {
        compile project(":koma-core")
        compile "org.jblas:jblas:1.2.3"
    }
}

project(':koma-tests') {
    subproj(project)
    
    dependencies {
        testCompile project(":koma-core")
        testCompile project(":backend-matrix-ejml")
        testCompile project(":backend-matrix-jblas")
        testCompile project(":backend-matrix-mtj")
        testCompile "junit:junit:4.12"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }
    test {
        // Some natives are broken on some systems, and our goal isn't to test netlib's backends
        jvmArgs '-Dcom.github.fommil.netlib.BLAS=com.github.fommil.netlib.F2jBLAS',
                '-Dcom.github.fommil.netlib.LAPACK=com.github.fommil.netlib.F2jLAPACK',
                '-Dcom.github.fommil.netlib.ARPACK=com.github.fommil.netlib.F2jARPACK'
    }
}

// Enable making a fatjar with the root proj
apply plugin: "java"
repositories {
	jcenter()
}
dependencies {
	compile project(":koma-core")
	compile project(":koma-logging")
	compile project(":backend-matrix-ejml")
	compile project(":backend-matrix-jblas")
	compile project(":backend-matrix-mtj")
}
