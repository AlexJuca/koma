// Add kotlin support to gradle
buildscript {
    ext.kotlin_version = '1.0.1'
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.7"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'kotlin'

apply from: 'buildscripts/mixedlang.gradle'
apply from: 'buildscripts/fatjar.gradle'
apply from: 'buildscripts/documentation.gradle'

if (hasProperty('bintrayUser'))
    apply from: 'buildscripts/publishing.gradle'

group = "net.antcenter"
version = "0.4"

repositories {
    mavenCentral()
}

dependencies {
    // Depend on the std libs of all our languages
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Different matrix backends
    compile 'org.ejml:all:0.27'
    compile 'org.jblas:jblas:1.2.3'
    compile 'com.github.fommil.netlib:all:1.1.2'
    compile('com.googlecode.matrix-toolkits-java:mtj:1.0.2') {
        // Exclude to fix gradle using a pom type as a jar
        exclude group: 'com.github.fommil.netlib'
    }

    compile 'org.slf4j:slf4j-api:1.7.21'

    // Unfortunately we have to import logback directly here to interop with MATLAB
    // (see golem.util.logging for more information). Note that outside of calling from MATLAB,
    // Golem will use the vanilla slf4j API and therefore golem users may use their own slf4j backend and
    // not use logback. Logback is only called directly in codepaths that support MATLAB interop.
    compile 'ch.qos.logback:logback-classic:1.1.7'

    // Plotting
    compile('org.knowm.xchart:xchart:2.6.0') {
        // Manually exclude an optional dependency that gradle is erroneously requiring (not on central)
        exclude module: 'VectorGraphics2D'
    }
    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}


task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Configure fatJar exclusions
project.ext.fatExcludes = ["native", "jblas"]
