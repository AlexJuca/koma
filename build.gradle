apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'scala'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

repositories {
  mavenCentral()
}

dependencies {
  // Depend on the std libs of all our languages
  compile 'org.jetbrains.kotlin:kotlin-stdlib:1.0.0-beta-1038'
  compile 'org.scala-lang:scala-library:2.11.4'

  // Different matrix backends
  compile 'org.ejml:all:0.27'
  compile 'org.jblas:jblas:1.2.3'
  compile 'com.github.fommil.netlib:all:1.1.2'
  compile('com.googlecode.matrix-toolkits-java:mtj:1.0.2') {
    // Exclude to fix gradle using a pom type as a jar
    exclude group: 'com.github.fommil.netlib'
  }

  // Plotting
  compile ('org.knowm.xchart:xchart:2.6.0') {
    // Manually exclude an optional dependency that gradle is erroneously requiring (not on central)
    exclude module: 'VectorGraphics2D'
  }

  testCompile 'junit:junit:4.12'

}

// Used non-standard mixed source layout (more logically organized)
sourceSets {
  main {
    java {
      srcDirs = ['src/main']
    }
    kotlin {
      srcDirs = ['src/main']
    }
    groovy {
      srcDirs = ['src/main']
    }
    scala {
      srcDirs = ['src/main']
    }
  }
  test {
    java {
      srcDirs = ['src/test']
    }
    kotlin {
      srcDirs = ['src/test']
    }
    groovy {
      srcDirs = ['src/test']
    }
    scala {
      srcDirs = ['src/test']
    }
  }
}

// Build a fatjar for MATLAB. Remember to build with JDK1.7 depending on MATLAB version
task fatJar(type: Jar) {
  baseName = project.name + '-all'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

// Add kotlin support to gradle
buildscript {
  ext.kotlin_version = '1.0.0-beta-1038'
  repositories {
    mavenCentral()
    mavenLocal()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0-beta-1038"
  }
}

// Deploy our artifact for distribution
publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'net.antcenter'
      artifactId 'golem'
      version '0.1'

      from components.java
    }
  }
  repositories {
    maven {
      url "$buildDir/dist"
    }
  }
}

